/**************************************************************************
*                                                                         *
*   PROJECT     : MIPS port for UCOS-II                                   *
*                                                                         *
*   MODULE      : bank a&b link.xn                                        *
*                                                                         *
*   AUTHOR      : wuyueqian                                               *
*                                                                         *
*   PROCESSOR   : MIPS 4KEc (32 bit RISC) - Actions board                 *
*                                                                         *
*   Tool-chain  : Cygnus                                                  *
*                                                                         *
*                                                                         *
*   Linker script file                                                    *
*                                                                         *
**************************************************************************/

/*定义GROUP BANK INDEX 和其他地址变量*/
INPUT(link_base.xn)

/*1.AP INDEX*/
AP_INDEX = FRONT_AP;

/*2.从地址map图中获取card模块的常驻代码段物理地址，只需写低14位，开发人员需填写，集成开始后只由集成人员修改，驱动人员不允许修改*/
SRAM_TEXT_ADDR = SRAM_AP_FRONT_RCODE_ADDR;
SRAM_DATA_ADDR = SRAM_AP_FRONT_DATA_ADDR;


/*3.转换为链接地址,不能修改*/
RCODE_TEXT_ADDR = RCODE_ADDR_BASE + SRAM_TEXT_ADDR;
RDATA_DATA_ADDR = RDATA_ADDR_BASE + SRAM_DATA_ADDR;

/*只是建议起始地址，也可以往后偏移，如control_2一般不是从这个地址，如果要占全部的空间，则使用control_1*/
BANK_CONTROL_1_ADDR_BASE = (AP_BANK_FRONT_CONTROL_1 << 24) + SRAM_AP_BANK_FRONT_CONTROL_ADDR;
/*back ap 没有UI段*/
BANK_UI_1_ADDR_BASE = (AP_BANK_FRONT_UI_1 << 24) + SRAM_AP_BANK_FRONT_UI_ADDR;
/*固定复用basal或者codec的空间，不能跨空间使用，跟control1和control2不一样*/
/*固定复用basal或者codec的空间，不能跨空间使用，跟control1和control2不一样*/
BANK_FRONT_ENHANCED_1_ADDR_BASE = (AP_BANK_FRONT_ENHANCED_1 << 24) + SRAM_AP_BANK_FRONT_ENHANCED_1_ADDR;
BANK_FRONT_ENHANCED_2_ADDR_BASE = (AP_BANK_FRONT_ENHANCED_2 << 24) + SRAM_AP_BANK_FRONT_ENHANCED_2_ADDR;

/*bank的实际空间大小,不能修改*/
BANK_CONTROL_SIZE = SRAM_AP_BANK_FRONT_CONTROL_SIZE;
BANK_UI_SIZE = SRAM_AP_BANK_FRONT_UI_SIZE;
BANK_FRONT_ENHANCED1_SIZE = SRAM_AP_BANK_FRONT_ENHANCED_1_SIZE;
BANK_FRONT_ENHANCED2_SIZE = SRAM_AP_BANK_FRONT_ENHANCED_2_SIZE;

/*系统允许的bank的最大空间大小，不能修改*/
AP_BANK_SPACE = BANK_SPACE;

OUTPUT_ARCH(mips)

ENTRY(__start)
EXTERN(key_op_entry)
EXTERN(audio_device_op_entry)

SECTIONS
{   
	. = 0x9fc19f80;
	APP_GLOBE_DATA :
    {
    	/*common globe 数据*/
    	applib_globe_data.o(.bss)
    	. = 0x80;
    }
          
	/* 常驻代码段: .text */
    .text RCODE_TEXT_ADDR	: 
    {
        api.a(.text .rodata)
        plist_rcode.o(.text .rodata .xdata)
        message_rcode.o(.text)
		/* scan_func.o(.text .rodata .xdata) */ /* 放入UI bank   */
        plist_fsel.o(.text .rodata .xdata)/*常驻代码,会占用后台AP_BACK_RCODE*/
        app_timer_rcode.o(.text)
    }
        /* 初始化了的全局数据段，紧跟在 .text段后面: .data */
    .data RDATA_DATA_ADDR	: AT(ADDR(.text) + SIZEOF(.text)) 
    {
        plist_rdata.o(.data) 
        eh_rdata.o(.data)
        eh_id3_rdata.o(.data)       
    }

	/* 未初始化的全局数据段，系统不会进行清零，由应用开发者自己把握: .bss, .sbss .common common */

    .bss	:
    {
        plist_rdata.o(.bss)    
        applib_app_data.o(.bss)
    	common_ui_data.o(.bss)
    	common_func_data.o(.bss)  
    	eh_rdata.o(.bss)
        eh_id3_rdata.o(.bss)    
        *(.sbss)
        *(.common)
        *(common)
    }

    /*6.链接bank fornt control 1代码*/
    /*bank0 预留给运行时库ctor */
    . = BANK_CONTROL_1_ADDR_BASE;
    /*OFFSET为ui bank 1组的实际物理地址*/
    OFFSET = . & 0x3ffff;
    BANK_CONTROL_1_0	: 
    {
        ctor.o(.text .rodata)
        . = BANK_CONTROL_SIZE; 
    }=0x0/*对定位符的赋值而产生的空隙由0x0填充*/
    
    . = ((. + AP_BANK_SPACE) & (~(AP_BANK_SPACE - 1))) + OFFSET;
    BANK_FORNT_CONTROL_1_1	: 
    {
        plist_main.o(.text .rodata .xdata)
        . = BANK_CONTROL_SIZE;
    }=0x0/*对定位符的赋值而产生的空隙由0x0填充*/

    . = ((. + AP_BANK_SPACE) & (~(AP_BANK_SPACE - 1))) + OFFSET;
    BANK_FORNT_CONTROL_1_2	: 
    {
        plist_scan_music.o(.text .rodata .xdata)
        . = BANK_CONTROL_SIZE;
    }=0x0
    
    . = ((. + AP_BANK_SPACE) & (~(AP_BANK_SPACE - 1))) + OFFSET;
    BANK_FORNT_CONTROL_1_3	: 
    {
        plist_scan_audible.o(.text .rodata .xdata)
        . = BANK_CONTROL_SIZE;
    }=0x0
    
    . = ((. + AP_BANK_SPACE) & (~(AP_BANK_SPACE - 1))) + OFFSET;
    BANK_FORNT_CONTROL_1_4	: 
    {
        plist_sort_music.o(.text .rodata .xdata)
        . = BANK_CONTROL_SIZE;
    }=0x0
    
    . = ((. + AP_BANK_SPACE) & (~(AP_BANK_SPACE - 1))) + OFFSET;
    BANK_FORNT_CONTROL_1_5	: 
    {
        plist_sort_audible.o(.text .rodata .xdata)
        . = BANK_CONTROL_SIZE;
    }=0x0
    
    . = ((. + AP_BANK_SPACE) & (~(AP_BANK_SPACE - 1))) + OFFSET;
    BANK_FORNT_CONTROL_1_6	: 
    {
        plist_other_list.o(.text .rodata .xdata)
        . = BANK_CONTROL_SIZE;
    }=0x0        

    . = ((. + AP_BANK_SPACE) & (~(AP_BANK_SPACE - 1))) + OFFSET;
    BANK_FORNT_CONTROL_1_7	: 
    {
        plist_operate_list.o(.text .rodata .xdata)
        . = BANK_CONTROL_SIZE;
    }=0x0     
        
    . = ((. + AP_BANK_SPACE) & (~(AP_BANK_SPACE - 1))) + OFFSET;
    BANK_FORNT_CONTROL_1_8	: 
    {
        plist_ui_deal.o(.text .rodata .xdata)
        . = BANK_CONTROL_SIZE;
    }=0x0
    
    . = ((. + AP_BANK_SPACE) & (~(AP_BANK_SPACE - 1))) + OFFSET;
    BANK_FORNT_CONTROL_1_9	: 
    {
        plist_m3u_deal.o(.text .rodata .xdata)
        . = BANK_CONTROL_SIZE;
    }=0x0

/*  放入enhanced bank1
    . = ((. + AP_BANK_SPACE) & (~(AP_BANK_SPACE - 1))) + OFFSET;
    BANK_FORNT_CONTROL_1_20	: 
    {
        plist_sort_method.o(.text .rodata .xdata)
    }=0x0       
    
*/

/*  放入enhanced bank2
    . = ((. + AP_BANK_SPACE) & (~(AP_BANK_SPACE - 1))) + OFFSET;
    BANK_FORNT_CONTROL_1_21	: 
    {
        plist_sort_al.o(.text .rodata .xdata)
    }=0x0     
*/

	/*AP部分链接模版：占用ui bank 1组的0-39，共40个bank*/
    . = BANK_UI_1_ADDR_BASE;
    /*OFFSET为ui bank 1组的实际物理地址*/
    OFFSET = . & 0x3ffff;
    BANK_UI_1_0	: 
    {
        plist_scan_func.o(.text .rodata .xdata)
        . = BANK_UI_SIZE;
    }=0x0
    
    . = ((. + AP_BANK_SPACE) & (~(AP_BANK_SPACE - 1))) + OFFSET;
    BANK_UI_1_1	: 
    {
        plist_creat_userpl.o(.text .rodata .xdata)
        . = BANK_UI_SIZE;
    }=0x0
    
    . = ((. + AP_BANK_SPACE) & (~(AP_BANK_SPACE - 1))) + OFFSET;
    BANK_UI_1_2	: 
    {
        plist_sys_deal.o(.text .rodata .xdata)
        . = BANK_UI_SIZE;
    }=0x0
    
    . = ((. + AP_BANK_SPACE) & (~(AP_BANK_SPACE - 1))) + OFFSET;
    BANK_UI_1_3	: 
    {
        plist_m3u_deal_sub.o(.text .rodata .xdata)
        . = BANK_UI_SIZE;
    }=0x0
    
    . = ((. + AP_BANK_SPACE) & (~(AP_BANK_SPACE - 1))) + OFFSET;
    BANK_UI_1_4	: 
    {
        plist_creat_bmk.o(.text .rodata .xdata)
        . = BANK_UI_SIZE;
    }=0x0
    
    . = ((. + AP_BANK_SPACE) & (~(AP_BANK_SPACE - 1))) + OFFSET;
    BANK_UI_1_5	: 
    {
        plist_m3u_deal_sub1.o(.text .rodata .xdata)
        . = BANK_UI_SIZE;
    }=0x0
    

/****确认没使用ENHANCED模块接口时可使用其BANK 空间，慎用！！！**********/
    
    /*排序部分：占用 ENHANCED 1组的1个bank*/ 
    . = BANK_FRONT_ENHANCED_1_ADDR_BASE + AP_BANK_SPACE * 32;
  /*OFFSET 为FRONT_BANK 1组的实际物理地址*/
    OFFSET = . & 0x3ffff;      
    BANK_FORNT_ENHANCED_1_32	: 
    {
        plist_sort_method.o(.text .rodata .xdata)
        . = BANK_FRONT_ENHANCED1_SIZE;
    }=0x0      
	. = ((. + AP_BANK_SPACE) & (~(AP_BANK_SPACE - 1))) + OFFSET;  
	
	/*收藏夹更新：占用 ENHANCED 1组的1个bank*/       
    BANK_FORNT_ENHANCED_1_33	: 
    {
        plist_creat_userpl_sub.o(.text .rodata .xdata)
        . = BANK_FRONT_ENHANCED1_SIZE;
    }=0x0      
	. = ((. + AP_BANK_SPACE) & (~(AP_BANK_SPACE - 1))) + OFFSET;  
	
	
	/*排序部分：占用 ENHANCED 2组的1个bank*/    
	. = BANK_FRONT_ENHANCED_2_ADDR_BASE + AP_BANK_SPACE * 32;
  /*OFFSET 为FRONT_BANK 2组的实际物理地址*/
    OFFSET = . & 0x3ffff;
    BANK_FORNT_ENHANCED_2_32	: 
    {
        plist_sort_al.o(.text .rodata .xdata) 
        . = BANK_FRONT_ENHANCED2_SIZE;
    }=0x0 
    . = ((. + AP_BANK_SPACE) & (~(AP_BANK_SPACE - 1))) + OFFSET;    

}
