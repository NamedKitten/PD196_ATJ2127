/**************************************************************************
*                                                                         *
*   PROJECT     : MIPS port for UCOS-II                                   *
*                                                                         *
*   MODULE      : bank a&b link.xn                                        *
*                                                                         *
*   AUTHOR      : wuyueqian & lzcai                                       *
*                                                                         *
*   PROCESSOR   : MIPS 4KEc (32 bit RISC) - Actions board                 *
*                                                                         *
*   Tool-chain  : Cygnus                                                  *
*                                                                         *
*                                                                         *
*   Linker script file                                                    *
*   规定：AP文件与com-子流程放在 CONTROL 组；com-功能接口放在 UI 组       *
**************************************************************************/

/*定义GROUP BANK INDEX 和其他地址变量*/
INPUT(link_base.xn)

/*AP INDEX(modify)*/
AP_INDEX = FRONT_AP;

/*从地址map图中获取card模块的常驻代码段物理地址，只需写低14位，开发人员需填写，集成开始后只由集成人员修改，驱动人员不允许修改*/
SRAM_TEXT_ADDR = SRAM_AP_FRONT_RCODE_ADDR;
SRAM_DATA_ADDR = SRAM_AP_FRONT_DATA_ADDR;


/*转换为链接地址,不能修改*/
RCODE_TEXT_ADDR = RCODE_ADDR_BASE + SRAM_TEXT_ADDR;
RDATA_DATA_ADDR = RDATA_ADDR_BASE + SRAM_DATA_ADDR;

/*只是建议起始地址，也可以往后偏移，如control_2一般不是从这个地址，如果要占全部的空间，则使用control_1*/
BANK_CONTROL_1_ADDR_BASE = (AP_BANK_FRONT_CONTROL_1 << 24) + SRAM_AP_BANK_FRONT_CONTROL_ADDR;
/*back ap 没有UI段*/
BANK_UI_1_ADDR_BASE = (AP_BANK_FRONT_UI_1 << 24) + SRAM_AP_BANK_FRONT_UI_ADDR;
/*固定复用basal或者codec的空间，不能跨空间使用，跟control1和control2不一样*/
BANK_FRONT_ENHANCED_1_ADDR_BASE = (AP_BANK_FRONT_ENHANCED_1 << 24) + SRAM_AP_BANK_FRONT_ENHANCED_1_ADDR;
BANK_FRONT_ENHANCED_2_ADDR_BASE = (AP_BANK_FRONT_ENHANCED_2 << 24) + SRAM_AP_BANK_FRONT_ENHANCED_2_ADDR;

BANK_BACK_ENHANCED_1_ADDR_BASE = (AP_BANK_BACK_ENHANCED_1 << 24) + SRAM_AP_BANK_BACK_ENHANCED_1_ADDR;
BANK_BACK_ENHANCED_2_ADDR_BASE = (AP_BANK_BACK_ENHANCED_2 << 24) + SRAM_AP_BANK_BACK_ENHANCED_2_ADDR;
/*bank的实际空间大小,不能修改*/
BANK_CONTROL_SIZE = SRAM_AP_BANK_FRONT_CONTROL_SIZE;
BANK_UI_SIZE = SRAM_AP_BANK_FRONT_UI_SIZE;
/*applib 和 一些快捷接口所在段的段大小定为1.5KB，这样可以保留0.5KB作为bank data缓冲区*/
BANK_UI_RESERVE_SIZE = SRAM_AP_BANK_FRONT_UI_SIZE - 0x200;
BANK_FRONT_ENHANCED1_SIZE = SRAM_AP_BANK_FRONT_ENHANCED_1_SIZE;
BANK_FRONT_ENHANCED2_SIZE = SRAM_AP_BANK_FRONT_ENHANCED_2_SIZE;
BANK_BACK_ENHANCED1_SIZE = SRAM_AP_BANK_BACK_ENHANCED_1_SIZE;
BANK_BACK_ENHANCED2_SIZE = SRAM_AP_BANK_BACK_ENHANCED_2_SIZE;

/*系统允许的bank的最大空间大小，不能修改*/
AP_BANK_SPACE = BANK_SPACE;

OUTPUT_ARCH(mips)

ENTRY(__start)
EXTERN(key_op_entry)

SECTIONS
{   
	. = 0x9fc19f80;
	APP_GLOBE_DATA :
    {
    	/*common globe 数据*/
    	applib_globe_data.o(.bss)
    	. = 0x80;
    }
	
	/* 常驻代码段: .text */
    .text RCODE_TEXT_ADDR	: 
    {
        _resident_text = ABSOLUTE(.); /*只是标号*/
        /*填写rcode输入节*/
        /*psp api接口*/
        api.a(.text)
        /*common 常驻代码*/
        app_timer_rcode.o(.text)
        message_rcode.o(.text)
        _resident_text_end = ABSOLUTE(.);
    }

    /* 初始化了的全局数据段，紧跟在 .text段后面: .data */
    .data RDATA_DATA_ADDR	: AT(ADDR(.text) + SIZEOF(.text)) 
    {
        _resident_data = ABSOLUTE(.);
        /*填写rdata输入节，所以文件产生的.data都被链接到这里*/
        alarm_main.o(.data)
        *.o(.data)
        _resident_data_end = ABSOLUTE(.);
    }

	/* 未初始化的全局数据段，由系统初始为0 */
    .bss	:
    {
    	/*common 全局数据*/
        alarm_main.o(.bss)
    	applib_app_data.o(.bss)
    	common_ui_data.o(.bss)
    	common_func_data.o(.bss)
    	/*enhanced 全局数据*/
    	eh_bs_rdata.o(.bss)
    	eh_rdata.o(.bss)
        eh_fsel_rdata.o(.bss)
        eh_id3_rdata.o(.bss)
        *(.sbss)
        *(.common)/*小普通符号*/
        *(common)/*标准普通符号*/
    }

    /*AP部分：占用bank control 1组的0-47，共48个bank*/
    /*bank0 预留给运行时库ctor */
    . = BANK_CONTROL_1_ADDR_BASE;
    /*OFFSET为control bank 1组的实际物理地址*/
    OFFSET = . & 0x3ffff;
    BANK_CONTROL_1_0	: 
    {
        ctor.o(.text .rodata .xdata)
        . = BANK_CONTROL_SIZE;
    }

    . = ((. + AP_BANK_SPACE) & (~(AP_BANK_SPACE - 1))) + OFFSET;
    BANK_CONTROL_1_1	: 
    {
        alarm_main.o(.text .rodata .xdata)
        . = BANK_CONTROL_SIZE;
    }
    
        /*另一个bank control 1*/
    . = ((. + AP_BANK_SPACE) & (~(AP_BANK_SPACE - 1))) + OFFSET;
    BANK_CONTROL_1_2	: 
    {
        alarm_playing_init.o(.text .rodata)
        . = BANK_CONTROL_SIZE;
    }
    
    /*另一个bank control 1*/
    . = ((. + AP_BANK_SPACE) & (~(AP_BANK_SPACE - 1))) + OFFSET;
    BANK_CONTROL_1_3	: 
    {
        alarm_playing_loopdeal.o(.text .rodata)
        . = BANK_CONTROL_SIZE;
    }
    /*另一个bank control 1*/
    . = ((. + AP_BANK_SPACE) & (~(AP_BANK_SPACE - 1))) + OFFSET;
    BANK_CONTROL_1_4	: 
    {
        alarm_playing_paint.o(.text .rodata .xdata)
        . = BANK_CONTROL_SIZE;
    }
    /*
    . = ((. + AP_BANK_SPACE) & (~(AP_BANK_SPACE - 1))) + OFFSET;
    BANK_CONTROL_1_5	: 
    {
        alarm_playing_keymsg.o(.text .rodata .xdata)
    }
    */
    /*control_1_1和control_2_0同时存在*/
    /*. = ((. + AP_BANK_SPACE) & (~(AP_BANK_SPACE - 1))) + OFFSET;*/
    /*BANK_CONTROL_1_13	: */
    /*{*/
        /*xxx.o(.text .rodata)*/
    /*    menu_callback_stopwatch_tools_2.o(.text .rodata)*/
        /*. = BANK_CONTROL_SIZE;*/
    /*}*/

 
}
