/**************************************************************************
*                                                                         *
*   PROJECT     : MIPS port for UCOS-II                                   *
*                                                                         *
*   MODULE      : bank a&b link.xn                                        *
*                                                                         *
*   AUTHOR      : wuyueqian                                               *
*                                                                         *
*   PROCESSOR   : MIPS 4KEc (32 bit RISC) - Actions board                 *
*                                                                         *
*   Tool-chain  : Cygnus                                                  *
*                                                                         *
*                                                                         *
*   Linker script file                                                    *
*                                                                         *
**************************************************************************/

/*定义GROUP BANK INDEX 和其他地址变量*/
INPUT(link_base.xn)

/*1.AP INDEX*/
AP_INDEX = FRONT_AP;

/*2.从地址map图中获取card模块的常驻代码段物理地址，只需写低14位，开发人员需填写，集成开始后只由集成人员修改，驱动人员不允许修改*/
SRAM_TEXT_ADDR = SRAM_AP_BANK_BACK_CODEC_ADDR;
SRAM_DATA_ADDR = SRAM_AP_FRONT_DATA_ADDR;

/*3.转换为链接地址,不能修改*/
RCODE_TEXT_ADDR = RCODE_ADDR_BASE + SRAM_TEXT_ADDR;
RDATA_DATA_ADDR = RDATA_ADDR_BASE + SRAM_DATA_ADDR;

/*只是建议起始地址，也可以往后偏移，如control_2一般不是从这个地址，如果要占全部的空间，则使用control_1*/
BANK_CONTROL_1_ADDR_BASE = (AP_BANK_FRONT_CONTROL_1 << 24) + SRAM_AP_BANK_FRONT_CONTROL_ADDR;
/*back ap 没有UI段*/
BANK_UI_1_ADDR_BASE = (AP_BANK_FRONT_UI_1 << 24) + SRAM_AP_BANK_FRONT_UI_ADDR;
/*固定复用basal或者codec的空间，不能跨空间使用，跟control1和control2不一样*/
BANK_ENHANCED_1_ADDR_BASE = (AP_BANK_FRONT_ENHANCED_1 << 24) + SRAM_AP_BANK_FRONT_ENHANCED_1_ADDR;
BANK_ENHANCED_2_ADDR_BASE = (AP_BANK_FRONT_ENHANCED_2 << 24) + SRAM_AP_BANK_FRONT_ENHANCED_1_ADDR;

/*bank的实际空间大小,不能修改*/
BANK_CONTROL_SIZE = SRAM_AP_BANK_FRONT_CONTROL_SIZE;
BANK_UI_SIZE = SRAM_AP_BANK_FRONT_UI_SIZE;
/*applib 和 一些快捷接口所在段的段大小定为1.5KB，这样可以保留0.5KB作为bank data缓冲区*/
BANK_UI_RESERVE_SIZE = SRAM_AP_BANK_FRONT_UI_SIZE - 0x200;
BANK_ENHANCED1_SIZE = SRAM_AP_BANK_FRONT_ENHANCED_1_SIZE;
BANK_ENHANCED2_SIZE = SRAM_AP_BANK_FRONT_ENHANCED_2_SIZE;

/*系统允许的bank的最大空间大小，不能修改*/
AP_BANK_SPACE = BANK_SPACE;

OUTPUT_ARCH(mips)

ENTRY(__start)
EXTERN(key_op_entry)
EXTERN(audio_device_op_entry)

SECTIONS
{
    . = 0x9fc19f80;
	APP_GLOBE_DATA :
    {
    	/*common globe 数据*/
    	applib_globe_data.o(.bss)
    	. = 0x80;
    }
    
	  /* 常驻代码段: .text */
    .text RCODE_TEXT_ADDR  : 
    {
        _resident_text = ABSOLUTE(.); /*只是标号*/
        /*4.填写rcode输入节*/
        api.a(.text  .rodata)
        /*common 常驻代码*/
        app_timer_rcode.o(.text  .rodata)
        message_rcode.o(.text  .rodata)
        main_udisk.o(.text  .rodata)
        udisk_task.o(.text  .rodata)
        udisk_enter2exit.o(.text  .rodata)  
        _resident_text_end = ABSOLUTE(.);        
    }
    
    /* 初始化了的全局数据段，紧跟在 .text段后面: .data */
    .data RDATA_DATA_ADDR	: AT(ADDR(.text) + SIZEOF(.text)) 
    {
        _resident_data = ABSOLUTE(.);
        /*5.填写rdata输入节，所以文件产生的.data都被链接到这里*/
        main_udisk.o(.data)
        _resident_data_end = ABSOLUTE(.);
    }

	/* 未初始化的全局数据段，系统不会进行清零，由应用开发者自己把握: .bss, .sbss .common common */
    .bss	:
    {
        /*common 全局数据*/
    	applib_app_data.o(.bss)
    	common_ui_data.o(.bss)
    	common_func_data.o(.bss)
    	main_udisk.o(.bss)
        *(.sbss)
        *(.common)/*小普通符号*/
        *(common)/*标准普通符号*/
    }

    /*6.链接bank control 1代码*/
    /*bank0 预留给运行时库ctor */
    . = BANK_CONTROL_1_ADDR_BASE;
    /*OFFSET为control bank 1组的实际物理地址*/
    OFFSET = . & 0x3ffff;
    
    BANK_CONTROL_1_0    : 
    {
        ctor.o(.text .rodata)        
    }
    
    /*common-功能接口部分：分配在ui bank 1组的16-63，共48 bank*/
    . = BANK_UI_1_ADDR_BASE + AP_BANK_SPACE * 16;
    /*OFFSET为ui bank 1组的实际物理地址*/
    OFFSET = . & 0x3ffff;
    . = . + 0x200;
    BANK_UI_1_16    : 
    {
        app_manager_bank.o(.text .rodata .xdata)
        . = BANK_UI_RESERVE_SIZE;
    }
    
    . = ((. + AP_BANK_SPACE) & (~(AP_BANK_SPACE - 1))) + OFFSET + 0x200;
    BANK_UI_1_17    : 
    {
        app_timer_bank.o(.text .rodata .xdata)
        . = BANK_UI_RESERVE_SIZE;
    }
    
    . = ((. + AP_BANK_SPACE) & (~(AP_BANK_SPACE - 1))) + OFFSET + 0x200;
    BANK_UI_1_18    : 
    {
        message_bank_init.o(.text .rodata .xdata)
        message_bank_send.o(.text .rodata .xdata)
        message_bank_broadcast.o(.text .rodata .xdata)
        . = BANK_UI_RESERVE_SIZE;
    }
    
    . = ((. + AP_BANK_SPACE) & (~(AP_BANK_SPACE - 1))) + OFFSET + 0x200;
    BANK_UI_1_19    : 
    {
        /* 去掉软看门狗部分 */
        . = BANK_UI_RESERVE_SIZE;
    }
    
    . = ((. + AP_BANK_SPACE) & (~(AP_BANK_SPACE - 1))) + OFFSET + 0x200;
    BANK_UI_1_20    : 
    {
        common_config.o(.text .rodata .xdata)
        . = BANK_UI_RESERVE_SIZE;
    }
    
    . = ((. + AP_BANK_SPACE) & (~(AP_BANK_SPACE - 1))) + OFFSET + 0x200;
    BANK_UI_1_21    : 
    {
        common_msgmap_data.o(.text .rodata .xdata)
        common_msgmap.o(.text .rodata .xdata)
        . = BANK_UI_RESERVE_SIZE;
    }
    
    . = ((. + AP_BANK_SPACE) & (~(AP_BANK_SPACE - 1))) + OFFSET + 0x200;
    BANK_UI_1_22    : 
    {
        common_key.o(.text .rodata .xdata)
        common_msg.o(.text .rodata .xdata)
        . = BANK_UI_RESERVE_SIZE;
    }
    
    . = ((. + AP_BANK_SPACE) & (~(AP_BANK_SPACE - 1))) + OFFSET + 0x200;
    BANK_UI_1_23    : 
    {
        /* 去掉软看门狗部分 */
        . = BANK_UI_RESERVE_SIZE;
    }
    
    . = ((. + AP_BANK_SPACE) & (~(AP_BANK_SPACE - 1))) + OFFSET + 0x200;
    BANK_UI_1_24    : 
    {
        miscellaneous.o(.text .rodata .xdata)
        . = BANK_UI_RESERVE_SIZE;
    }
}
