/**************************************************************************
*                                                                         *
*   PROJECT     : MIPS port for UCOS-II                                   *
*                                                                         *
*   MODULE      : bank a&b link.xn                                        *
*                                                                         *
*   AUTHOR      : wuyueqian                                               *
*                                                                         *
*   PROCESSOR   : MIPS 4KEc (32 bit RISC) - Actions board                 *
*                                                                         *
*   Tool-chain  : Cygnus                                                  *
*                                                                         *
*                                                                         *
*   Linker script file                                                    *
*                                                                         *
**************************************************************************/
/************************ memory map********************************
*-----------------------------ap code-------------------------------------*
*---rcode        :0xbfc1ad00-0xbfc1adeb    length:0xec                    *  
*---bank UI      :(0x7f**0000+0x1ae00)-(0x7f**0000+0x1afff)  length:0x200 *
*----------------------------ap data--------------------------------------*
*--rdata: 0x9fc1adec-0x9fc1d000 length:rcode+rdata=0x100                  *
*--applib:0x9fc19f80-0x9fc19fff length:0x80                               *  
***************************************************************************/
/*定义GROUP BANK INDEX 和其他地址变量*/
INPUT(link_base.xn)
INPUT(sdk_link_base.xn)

/*1.AP INDEX*/
AP_INDEX = PROCESS_MANAGER_AP;

/*2.从地址map图中获取card模块的常驻代码段物理地址，只需写低14位，开发人员需填写，集成开始后只由集成人员修改，驱动人员不允许修改*/
SRAM_TEXT_ADDR = SRAM_AP_MANAGER_ADDR;
/*SRAM_DATA_ADDR = SRAM_OS_DATA_ADDR + 0x1000 - 0x200;*/


/*3.转换为链接地址,不能修改*/
RCODE_TEXT_ADDR = RCODE_ADDR_BASE + SRAM_TEXT_ADDR;
/*RDATA_DATA_ADDR = RDATA_ADDR_BASE + SRAM_DATA_ADDR;*/

/*只是建议起始地址，也可以往后偏移，如control_2一般不是从这个地址，如果要占全部的空间，则使用control_1*/
BANK_MANAGER_BASE = (AP_BANK_MANAGER << 24) + SRAM_AP_BANK_MANAGER_ADDR;

/*bank的实际空间大小,不能修改*/
BANK_MANAGER_SIZE = SRAM_AP_BANK_MANAGER_SIZE;


/*系统允许的bank的最大空间大小，不能修改*/
AP_BANK_SPACE = BANK_SPACE;

OUTPUT_ARCH(mips)

ENTRY(__start)

SECTIONS
{
    . = 0x9fc19f80;
	APP_GLOBE_DATA :
    {
    	/*common globe 数据*/
    	applib_globe_data.o(.bss)
    	. = 0x80;
    }
    
    /*常驻代码*/
    .text RCODE_TEXT_ADDR  : 
    {
        manager_msg_loop.o(.text .rodata)            
        api.a(.text)         
    }

    . = (. - 0x20000000);
    .data    : 
    {
        manager_main.o(.data)
    }

    .bss :
    {
        manager_main.o(.bss)  
        *(.sbss)
        *(.common)
        *(common)
    }  

    . = BANK_MANAGER_BASE;
    OFFSET = . & 0x3ffff;
    
    BANK_MANAGER_0    : 
    {
        ctor.o(.text .rodata .xdata)           
        . = 0x200;    
    }   
    
    . = ((. + AP_BANK_SPACE) & (~(AP_BANK_SPACE - 1))) + OFFSET;
    BANK_MANAGER_1    : 
    {       
        manager_main.o(.text .rodata .xdata)            
        . = 0x200;
          
    }  
    
    . = ((. + AP_BANK_SPACE) & (~(AP_BANK_SPACE - 1))) + OFFSET;
    BANK_MANAGER_2    : 
    {                  
        manager_config.o(.text .rodata .xdata)  
        . = 0x200;
          
    }     
    
    . = ((. + AP_BANK_SPACE) & (~(AP_BANK_SPACE - 1))) + OFFSET;
    BANK_MANAGER_3    : 
    {              
        manager_applib_funs.o(.text .rodata .xdata) 
        . = 0x200; 
    }     
    
    . = ((. + AP_BANK_SPACE) & (~(AP_BANK_SPACE - 1))) + OFFSET;
    BANK_MANAGER_4    : 
    {           
        manager_message_send.o(.text .rodata .xdata)  
        . = 0x200;         
    }   
    
    . = ((. + AP_BANK_SPACE) & (~(AP_BANK_SPACE - 1))) + OFFSET;
    BANK_MANAGER_5    : 
    {            
        manager_get_name.o(.text .rodata .xdata)  
        . = 0x200;         
    }   
    
    . = ((. + AP_BANK_SPACE) & (~(AP_BANK_SPACE - 1))) + OFFSET;
    BANK_MANAGER_6    : 
    {            
        manager_msg_handle.o(.text .rodata .xdata) 
        . = 0x200;                     
    }   
    
    . = ((. + AP_BANK_SPACE) & (~(AP_BANK_SPACE - 1))) + OFFSET;
    BANK_MANAGER_7    : 
    {            
        manager_msg_handle2.o(.text .rodata .xdata) 
        . = 0x200;                      
    }   
      
}
